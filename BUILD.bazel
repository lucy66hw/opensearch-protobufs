load("@io_bazel_rules_go//go:def.bzl", "go_library")
load("@versioning//:version.bzl", "VERSION")
load("@rules_proto_grpc//python:defs.bzl", "python_proto_library", "python_grpc_library")
load("@rules_python//python:packaging.bzl", "py_wheel")

package(default_visibility = ["//visibility:public"])

java_library(
    name = "java_protos_all",
    visibility = ["//visibility:public"],
    exports = [
        "//protos/schemas:common_java_proto",
        "//protos/schemas:document_java_proto",
        "//protos/schemas:search_java_proto",
        "//protos/services:document_service_java_proto",
        "//protos/services:search_service_java_proto",
    ],
)

python_proto_library(
    name = "python_schemas",
    visibility = ["//visibility:public"],
    protos = [
        "//protos/schemas:common_proto",
        "//protos/schemas:document_proto",
        "//protos/schemas:search_proto",
    ],
)

python_grpc_library(
    name = "python_services",
    visibility = ["//visibility:public"],
    protos = [
        "//protos/services:document_service_proto",
        "//protos/services:search_service_proto",
    ],
    deps = [
        ":python_schemas"
    ]
)

go_library(
    name = "go_protos_all",
    visibility = ["//visibility:public"],
    deps = [
        "//protos/schemas:common_go_proto",
        "//protos/schemas:document_go_proto",
        "//protos/schemas:search_go_proto",
        "//protos/services:document_service_go_proto",
        "//protos/services:search_service_go_proto",
    ],
)

"""
Package python libraries into a wheel.
Protoc does not provide a package option for python so we must fix the import statements in these proto files manually:
https://github.com/protocolbuffers/protobuf/issues/7061
https://github.com/protocolbuffers/protobuf/issues/2283
Additionally rules_proto_grpc 4.5.0 (and all pre 5.0.0/bazel mod versions) do not support pyi generation.
To supply debug symbols an additional genrule is needed to compile them manually with protoc.
"""

genrule(
    name = "generate_pyi_files",
    srcs = [
        "//protos/schemas:common.proto",
        "//protos/schemas:document.proto",
        "//protos/schemas:search.proto",
        "//protos/services:document_service.proto",
        "//protos/services:search_service.proto",
        "@com_google_protobuf//:well_known_type_protos",
    ],
    outs = [
        "python_pyi/schemas/common_pb2.pyi",
        "python_pyi/schemas/document_pb2.pyi", 
        "python_pyi/schemas/search_pb2.pyi",
        "python_pyi/services/document_service_pb2.pyi",
        "python_pyi/services/search_service_pb2.pyi",
    ],
    cmd = """        
        # Use protoc with --pyi_out to generate debug symbols
        $(location @com_google_protobuf//:protoc) \
            --proto_path=. \
            --proto_path=external/com_google_protobuf/src \
            --pyi_out=$(RULEDIR) \
            protos/schemas/common.proto \
            protos/schemas/document.proto \
            protos/schemas/search.proto \
            protos/services/document_service.proto \
            protos/services/search_service.proto

        mkdir -p $(RULEDIR)/python_pyi/
        mv $(RULEDIR)/protos/schemas $(RULEDIR)/python_pyi
        mv $(RULEDIR)/protos/services $(RULEDIR)/python_pyi
    """,
    tools = ["@com_google_protobuf//:protoc"],
)

genrule(
    name = "fix_python_imports",
    srcs = [":python_schemas", ":python_services", ":generate_pyi_files"],
    outs = [
        "opensearch/protobufs/schemas/common_pb2.py",
        "opensearch/protobufs/schemas/common_pb2.pyi",
        "opensearch/protobufs/schemas/document_pb2.py",
        "opensearch/protobufs/schemas/document_pb2.pyi",
        "opensearch/protobufs/schemas/search_pb2.py",
        "opensearch/protobufs/schemas/search_pb2.pyi",
        "opensearch/protobufs/services/document_service_pb2.py",
        "opensearch/protobufs/services/document_service_pb2.pyi",
        "opensearch/protobufs/services/document_service_pb2_grpc.py",
        "opensearch/protobufs/services/search_service_pb2.py",
        "opensearch/protobufs/services/search_service_pb2.pyi",
        "opensearch/protobufs/services/search_service_pb2_grpc.py",
        "opensearch/protobufs/schemas/__init__.py",
        "opensearch/protobufs/services/__init__.py",
    ],
    cmd = """
        mkdir -p $(RULEDIR)/opensearch/protobufs/
        
        cp -rL $(BINDIR)/python_schemas_pb/protos/schemas $(RULEDIR)/opensearch/protobufs
        cp -rL $(BINDIR)/python_services_pb/protos/services $(RULEDIR)/opensearch/protobufs

        cp -rL $(BINDIR)/python_pyi/schemas $(RULEDIR)/opensearch/protobufs
        cp -rL $(BINDIR)/python_pyi/services $(RULEDIR)/opensearch/protobufs

        # Find and replace "from protos" with "from opensearch.protobufs" in all files
        find $(RULEDIR)/opensearch/protobufs/ -type f -exec sed -i 's/from protos/from opensearch.protobufs/g' {} +
        
        touch $(RULEDIR)/opensearch/protobufs/schemas/__init__.py
        touch $(RULEDIR)/opensearch/protobufs/services/__init__.py
    """,
)

py_wheel(
    name = "opensearch_protos_wheel",
    distribution = "opensearch-protobufs",
    python_tag = "py3",
    version = VERSION,
    requires = [
        "protobuf>=3.20.3",
        "grpcio>=1.68.1",
    ],
    author = "OpenSearch Team",
    license = "Apache-2.0",
    classifiers = [
        "Intended Audience :: Developers",
        "License :: OSI Approved :: Apache Software License",
        "Programming Language :: Python :: 3",
    ],
    homepage = "https://opensearch.org/",
    project_urls = {
        "Bug Tracker": "https://github.com/opensearch-project/opensearch-protobufs/issues",
        "Documentation": "https://github.com/opensearch-project/opensearch-protobufs/blob/main/README.md",
        "Source Code": "https://github.com/opensearch-project/opensearch-protobufs",
    },
    platform = "any",
    python_requires = ">=3.10",
    deps = [
        ":fix_python_imports",
    ],
)
